Import( Split( 'name root' ) )

### executable #################################################################
build = root.Clone()
build.Append( LIBS = Split( 'executable io ipc parallel registry' ) )

if name == 'posix':
	build.Append( RPATH = "'$$$${ORIGIN}/../library'" )
elif name == 'darwin':
	build.Append( LINKFLAGS = '-Wl,-rpath,@executable_path/../library' )

out = build.Program( '#binary/registry', 'main.cpp' )
build.SideEffect( 'main.rpo', out )
build.Clean( out, '#binary/registry.log' )

### library ####################################################################
build = root.Clone()

if name == 'darwin':
	build.Append( LINKFLAGS = '-Wl,-install_name,@rpath/libregistry.dylib' )

glob = Glob( '*.cpp', strings = True )
glob.remove( 'main.cpp' )
out = build.SharedLibrary( '#library/libregistry', glob )
glob = Glob( '*.rpo', strings = True )
glob.remove( 'main.rpo' )
build.SideEffect( glob, out )
