Import( Split( 'name ooe platform root' ) )

### configure ##################################################################
build = root.Clone()
build.Append( CPPPATH = ooe.freetype.include_path )
build.Append( LIBPATH = ooe.freetype.library_path )

if not build.GetOption( 'clean' ):
	setup = Configure( build )

	if not setup.CheckLibWithHeader( 'freetype', 'ft2build.h', 'c' ):
		Exit( 1 )

	if name == 'posix':
		if not setup.CheckLib( ooe.x11.library ): Exit( 1 )

	build = setup.Finish()

### build ######################################################################
if name == 'darwin':
	build.Append( FRAMEWORKS = ooe.qtkit.framework )
	build.Append( LINKFLAGS = '-Wl,-install_name,@executable_path/../library/libgeneral.dylib' )

files = Glob( '*.cpp' ) + Glob( platform + '/foundation/general/*.cpp' )
files += Glob( platform + '/foundation/general/*.mm' )
out = build.SharedLibrary( '#library/libgeneral', files )
build.SideEffect( Glob( '*.rpo' ), out )
